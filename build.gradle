apply plugin: 'java'
apply plugin: 'maven'

if (project.hasProperty('signing.keyId')) {
    apply plugin: 'signing'
}

group = 'org.phoenixframework.channels'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '1.7.0'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.okhttp3:okhttp-ws:3.2.0'
    compile "com.google.code.gson:gson:2.6.2"
    compile "javax.inject:javax.inject:1"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

repositories {
    mavenCentral()
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Java Library for Phoenix Channel Clients'
                    packaging 'jar'
                    description 'Java Library for Phoenix Channel Clients'
                    url 'https://github.com/eoinsha/JavaPhoenixChannels'

                    scm {
                        connection 'scm:git:git://github.com/eoinsha/JavaPhoenixChannels.git'
                        developerConnection 'scm:git:git@github.com:eoinsha/JavaPhoenixChannels.git'
                        url 'https://github.com/eoinsha/JavaPhoenixChannels'
                    }

                    licenses {
                        license {
                            name 'Modified BSD License'
                            url 'http://directory.fsf.org/wiki/License:BSD_3Clause'
                        }
                    }

                    developers {
                        developer {
                            id 'eoinsha'
                            name 'Eoin Shanaghy'
                        }
                    }
                }
            }
        }
    }
}
